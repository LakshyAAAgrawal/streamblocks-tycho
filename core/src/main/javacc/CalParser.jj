
options {
	STATIC = false;
	CHOICE_AMBIGUITY_CHECK = 3;
	FORCE_LA_CHECK = false;
	DEBUG_PARSER = false;
	DEBUG_TOKEN_MANAGER = false;
	UNICODE_INPUT = true;
}



PARSER_BEGIN(CalParser)
package se.lth.cs.tycho.parsing.cal;

import java.lang.SuppressWarnings;
import java.util.List;
import java.util.ArrayList;
import java.util.Collections;
import java.util.Map;
import se.lth.cs.tycho.ir.*;
import se.lth.cs.tycho.ir.decl.*;
import se.lth.cs.tycho.ir.expr.*;
import se.lth.cs.tycho.ir.stmt.*;
import se.lth.cs.tycho.ir.stmt.lvalue.*;
import se.lth.cs.tycho.ir.entity.cal.*;
import se.lth.cs.tycho.ir.entity.nl.*;
import se.lth.cs.tycho.ir.entity.*;
import se.lth.cs.tycho.ir.ToolAttribute;
import se.lth.cs.tycho.ir.ToolValueAttribute;
import se.lth.cs.tycho.ir.ToolTypeAttribute;
import se.lth.cs.tycho.ir.util.ImmutableList;
import se.lth.cs.tycho.ir.util.ImmutableEntry;

@SuppressWarnings("unused")
public class CalParser {

	private boolean processDescription = false;

	public static class TypeAndName {
		public final TypeExpr type;
		public final String name;
		public TypeAndName(TypeExpr type, String name) {
			this.type = type;
			this.name = name;
		}
	}

	public static class NameAndDef<T> {
		public final String name;
		public final T def;
		public NameAndDef(String name, T def) {
			this.name = name;
			this.def = def;
		}
	}

	public <T extends AbstractIRNode> T setPosition(T node, Token from, Token to) {
		node.setPosition(from.beginLine, from.beginColumn, to.endLine, to.endColumn);
		return (T) node;
	}

	public <T extends AbstractIRNode> T setPosition(T node, IRNode from, IRNode to) {
		node.setPosition(from, to);
		return (T) node;
	}

	public <T extends AbstractIRNode> T setPosition(T node, Token from, IRNode to) {
		node.setPosition(from.beginLine, from.beginColumn, to.getToLineNumber(), to.getToColumnNumber());
		return (T) node;
	}

	public <T extends AbstractIRNode> T setPosition(T node, IRNode from, Token to) {
		node.setPosition(from.getFromLineNumber(), from.getFromColumnNumber(), to.endLine, to.endColumn);
		return (T) node;
	}
}
PARSER_END(CalParser)

/* Whitespace */
SKIP :
{
	" " | "\t" | "\n" | "\r" | "\f"
}


/* Comments */
MORE :
{
  < "//" (~["\n", "\r"])* >
|
  < "/" ("*")+ "*/" >
|
  < "/" "*" (<OP>)* > : IN_MULTI_LINE_COMMENT
}

<IN_MULTI_LINE_COMMENT>
SPECIAL_TOKEN :
{
  <MULTI_LINE_COMMENT: "*/" > : DEFAULT
}

<IN_MULTI_LINE_COMMENT,IN_SINGLE_LINE_COMMENT>
MORE :
{
  < ~[] >
}


/* Keywords */
TOKEN :
{
	< ACTION: "action" >
|	< ACTOR: "actor" >
|	< ALL: "all" >
|	< AND: "and" >
//|	< ANY: "any" >
//|	< AT: "at" >
//|	< AT_STAR: "at*" >
|	< BEGIN: "begin" >
//|	< CHOOSE: "choose" >
|	< CONST: "const" >
//|	< DELAY: "delay" >
|	< DIV: "div" >
|	< DO: "do" >
|	< DOM: "dom" >
|	< ELSE: "else" >
|	< ELSIF: "elsif" >
|	< END: "end" >
|	< ENDACTION: "endaction" >
|	< ENDACTOR: "endactor" >
|	< ENDCHOOSE: "endchoose" >
|	< ENDFOREACH: "endforeach" >
|	< ENDFUNCTION: "endfunction" >
|	< ENDIF: "endif" >
|	< ENDINITIALIZE: "endinitialize" >
|	< ENDLAMBDA: "endlambda" >
|	< ENDLET: "endlet" >
|	< ENDPRIORITY: "endpriority" >
|	< ENDPROC: "endproc" >
|	< ENDPROCEDURE: "endprocedure" >
|	< ENDSCHEDULE: "endschedule" >
|	< ENDWHILE: "endwhile" >
|	< ENTITY: "entity" >
|	< FALSE: "false" >
|	< FOR: "for" >
|	< FOREACH: "foreach" >
|	< FSM: "fsm" >
|	< FUNCTION: "function" >
|	< GUARD: "guard" >
|	< IF: "if" >
|	< IMPORT: "import" >
|	< IN: "in" >
|	< INITIALIZE: "initialize" >
|	< LAMBDA: "lambda" >
|	< LET: "let" >
|	< MAP: "map" >
|	< MOD: "mod" >
|	< MULTI: "multi" >
|	< MUTABLE: "mutable" >
|	< NAMESPACE: "namespace" >
|	< NOT: "not" >
|	< NULL: "null" >
|	< OLD: "old" >
|	< OR: "or" >
|	< PRIORITY: "priority" >
|	< PROC: "proc" >
|	< PROCEDURE: "procedure" >
|	< REGEXP: "regexp" >
|	< REPEAT: "repeat" >
|	< RNG: "rng" >
|	< SCHEDULE: "schedule" >
|	< THEN: "then" >
//|	< TIME: "time" >
|	< TRUE: "true" >
|	< TYPE: "type" >
|	< VAR: "var" >
|	< WHILE: "while" >

|	< PUBLIC: "public" >
|	< PRIVATE: "private" >
|	< LOCAL: "local" >

|	< NETWORK: "network" >
|	< ENTITIES: "entities" >
|	< STRUCTURE: "structure" >
|	< EXTERNAL: "external" >
}

/* Delimiters and separators */
TOKEN: {
	< COLON: ":" >
|	< DOT: "." >
|	< COMMA: "," >
|	< LONG_DOUBLE_ARROW_RITHT: "==>" >
|	< LONG_SINGLE_ARROW_RIGHT: "-->" >
|	< LONG_SINGLE_ARROW_LEFT: "<--" >
|	< LPAREN: "(" >
|	< RPAREN: ")" >
|	< LCURLY: "{" >
|	< RCURLY: "}" >
|	< LSQUARE: "[" >
|	< RSQUARE: "]" >
}

/* Operators */
TOKEN: {
	< EQ: "=" >
|	< COLON_EQ: ":=" >
|	< SINGLE_ARROW_RIGHT: "->" >
|	< VERTICAL_BAR: "|" >
|	< GREATER_THAN: ">" >
|	< STAR: "*" >
|	< DOT_STAR: ".*" >
|	< CINNAMON_BUN: "@" >
|	< OP: <OP_CHAR> (<OP_CHAR> | "=")* >
|	< #OP_CHAR: ["!", "#", "$", "%", "^", "&", "*", "/",
		"+", "-", "<", ">", "?", "~", "|", "."] >
}

/* String literals */
TOKEN: {
	< STRING : "\"" ((~["\""])* ("\\" "\"")*)* "\"" >
}


/* Numeric literals */
TOKEN: {
	< INTEGER: <DECIMAL_LITERAL> | <HEXADECIMAL_LITERAL> | <OCTAL_LITERAL> >
|
	<REAL:
			(<DECIMAL_DIGIT>)+ "." (<DECIMAL_DIGIT>)+ (<EXPONENT>)?
		|	(<DECIMAL_DIGIT>)+ "." <EXPONENT> >
//|
//	< REAL:
//			(<DECIMAL_DIGIT>)+ "." (<DECIMAL_DIGIT>)* (<EXPONENT>)?
//		|	"." (<DECIMAL_DIGIT>)+ (<EXPONENT>)?
//		|	(<DECIMAL_DIGIT>)+ <EXPONENT> >
|
	< #DECIMAL_LITERAL: <NON_ZERO_DECIMAL_DIGIT> (<DECIMAL_DIGIT>)* >
|
	< #HEXADECIMAL_LITERAL: "0" ["x", "X"] (<HEXADECIMAL_DIGIT>)+ >
|
	< #OCTAL_LITERAL: "0" (<OCTAL_DIGIT>)* >
|
	< #EXPONENT: ["e", "E"] (["+", "-"])? (<DECIMAL_DIGIT>)+ >
|
	< #NON_ZERO_DECIMAL_DIGIT: ["1"-"9"] >
|
	< #DECIMAL_DIGIT: ["0"-"9"] >
|
	< #OCTAL_DIGIT: ["0"-"7"] >
|
	< #HEXADECIMAL_DIGIT: ["0"-"9", "a"-"f", "A"-"F"] >
}


/* Identifies */
TOKEN: {
	< ID: (["a"-"z", "A"-"Z", "_", "$", "0"-"9"])+ >
}


QID NamespaceScan() :
{
	QID qid = QID.empty();
	Token unitId;
}
{
	(
		"namespace"
		qid = QID()
		":"
	)?
	{
		return qid;
	}
}


NamespaceDecl CompilationUnit() :
{
	NamespaceDecl ns;
}
{
	ns = NamespaceDeclContents(QID.empty())
	<EOF>
	{
		return ns;
	}
|
	ns = NamespaceDecl()
	<EOF>
	{
		return ns;
	}
}

NamespaceDecl OrccUnit() :
{
	Token id;
	ImmutableList.Builder<VarDecl> decls = ImmutableList.builder();
	VarDecl decl;
}
{
	"unit"
	id = <ID>
	":"
	(
		(Annotation())*
		decl = GlobalVarDecl()
		{ decls.add(decl); }
	)*
	"end"
	{
		return new NamespaceDecl(
				QID.of(id.image),
				ImmutableList.empty(),
				decls.build(),
				ImmutableList.empty(),
				ImmutableList.empty());
	}
}

Annotation Annotation() :
{
	Token id;
	Token par;
}
{
	"@" id = <ID>
	(
		"("
		(
			AnnotationParameter()
			("," AnnotationParameter())*
		)?
		")"
	)?
	{ return new Annotation(id.image, ImmutableList.empty()); }
}

void AnnotationParameter() :
{
}
{
	LOOKAHEAD(2)
	<ID> "=" Expression()
|
	Expression()
}

NamespaceDecl NamespaceDecl() :
{
	QID qid;
	NamespaceDecl ns;
}
{
	"namespace"
	qid = QID()
	":"
	ns = NamespaceDeclContents(qid)
	"end"
	{ return ns; }
}

NamespaceDecl NamespaceDeclContents(QID name) :
{
	ImmutableList.Builder<Import> imports = ImmutableList.builder();
	ImmutableList.Builder<VarDecl> varDecls = ImmutableList.builder();
	ImmutableList.Builder<EntityDecl> entityDecls = ImmutableList.builder();
	ImmutableList.Builder<TypeDecl> typeDecls = ImmutableList.builder();

	Import i;
	VarDecl v;
	EntityDecl e;
	TypeDecl t;
}
{
	(Annotation())*
	(
		i = Import() ";" { imports.add(i); }
	|
		LOOKAHEAD(2)
		v = GlobalVarDecl() { varDecls.add(v); }
	|
		e = ActorDecl() { entityDecls.add(e); }
	|
		e = NetworkDecl() { entityDecls.add(e); }
	)*
	{
		return new NamespaceDecl(name, imports.build(), varDecls.build(), entityDecls.build(), typeDecls.build());
	}
}

QID QID() :
{
	QID qid;
	Token id;
}
{
	id = <ID> { qid = QID.of(id.image); }
	(
		LOOKAHEAD(2)
		"."
		id = <ID> { qid = qid.concat(QID.of(id.image)); }
	)*
	{ return qid; }
}

/* Declarations */
Availability Availability() :
{
}
{
	"public" { return Availability.PUBLIC; }
|
	"private" { return Availability.PRIVATE; }
|
	"local" { return Availability.LOCAL; }
}

VarDecl GlobalVarDecl() :
{
	Availability availability = Availability.PUBLIC;
	String name;
	TypeExpr type = null;
	Expression expr;
}
{
	(availability = Availability())?
	(
		{ TypeAndName typeName; }
		typeName = VarDeclTypeName()
		"="
		expr = Expression()
		";"
		{ name = typeName.name; type = typeName.type; }
	|
		LOOKAHEAD(2)
		{ NameAndDef<ExprLambda> fun; }
		fun = FunDecl()
		{ name = fun.name; expr = fun.def; }
	|
		{ NameAndDef<ExprProc> proc; }
		proc = ProcDecl()
		{ name = proc.name; expr = proc.def; }
	)
	{
		return VarDecl.global(availability, type, name, expr);
	}
}


VarDecl LocalVarDecl(boolean endWithSemicolon) :
{
	TypeExpr type = null;
	String name;
	Expression expr = null;
	boolean assignable = false;
}
{
	(
		{ TypeAndName typeName; }
		typeName = VarDeclTypeName()
		(
			( "=" { assignable = false; } | ":=" { assignable = true; } )
			expr = Expression()
		)?
		(
			LOOKAHEAD({endWithSemicolon})
			";"
		)?
		{ name = typeName.name; type = typeName.type; }
	|
		LOOKAHEAD(2)
		{ NameAndDef<ExprLambda> fun; }
		fun = FunDecl()
		{ name = fun.name; expr = fun.def; }
	|
		{ NameAndDef<ExprProc> proc; }
		proc = ProcDecl()
		{ name = proc.name; expr = proc.def; }
	)
	{
		return VarDecl.local(type, name, !assignable, expr);
	}
}

Import Import() :
{
	Import imp;
}
{
	"import"
	(
		imp = GroupImportTail()
	|
		imp = SingleImportTail()
	)
	{
		return imp;
	}
}

GroupImport GroupImportTail() :
{
	QID globalName;
	Import.Kind kind = Import.Kind.VAR;
}
{
	"all"
	(kind = ImportKind())?
	globalName = QID()
	{
		return new GroupImport(kind, globalName);
	}
}

SingleImport SingleImportTail() :
{
	QID globalName;
	Import.Kind kind = Import.Kind.VAR;
	Token localNameToken = null;
}
{
	(kind = ImportKind())?
	globalName = QID()
	("=" localNameToken = <ID>)?
	{
		String localName;
		if (localNameToken == null) {
			localName = globalName.getLast().toString();
		} else {
			localName = localNameToken.image;
		}
		return new SingleImport(kind, globalName, localName);
	}
}

Import.Kind ImportKind() :
{}
{
	"var" { return Import.Kind.VAR; }
|
	"type" { return Import.Kind.TYPE; }
|
	"entity" { return Import.Kind.ENTITY; }
}

NameAndDef<ExprLambda> FunDecl() :
{
	boolean external = false;
	Token id;
	ImmutableList<VarDecl> valPars;
	ImmutableList<VarDecl> varDecls = ImmutableList.empty();
	Expression expr = null;
	TypeExpr type = null;
}
{
	("external" {external = true;})?
	"function"
	id = <ID>
	"("
	valPars = FormalValuePars()
	")"
	(
		"-->"
		type = Type()
	)?
	(
		"var"
		varDecls = VarDecls()
	)?
	(
		":"
		expr = Expression()
		"end"
	|
		";"
	|
		"end"
	)
	{
		if (!varDecls.isEmpty()) {
			expr = new ExprLet(ImmutableList.empty(), varDecls, expr);
		}
		ExprLambda lambda = new ExprLambda(ImmutableList.empty(), valPars, expr, type, external);
		return new NameAndDef<ExprLambda>(id.image, lambda);
	}
}

NameAndDef<ExprProc> ProcDecl() :
{
	Token id;
	ImmutableList<VarDecl> valPars;
	ImmutableList<VarDecl> varDecls = ImmutableList.empty();
	ImmutableList<Statement> stmts = ImmutableList.empty();
	boolean external = false;
}
{
	("external" {external=true;})?
	"procedure"
	id = <ID>
	"("
	valPars = FormalValuePars()
	")"
	(
		"var"
		varDecls = VarDecls()
	)?
	(
		";"
	|
		("begin" | "do")
		stmts = Statements()
		"end"
	|
		"end"
	)
	{
		ExprProc proc;
		if (external) {
			proc = new ExprProc(ImmutableList.empty(), valPars);
		} else if (varDecls.isEmpty()) {
			proc = new ExprProc(ImmutableList.empty(), valPars, stmts);
		} else {
			StmtBlock block = new StmtBlock(ImmutableList.empty(), varDecls, stmts);
			proc = new ExprProc(ImmutableList.empty(), valPars, ImmutableList.of(block));
		}
		return new NameAndDef<ExprProc>(id.image, proc);
	}
}

ImmutableList<VarDecl> VarDecls() :
{
	ImmutableList.Builder<VarDecl> builder = ImmutableList.builder();
	VarDecl decl;
}
{
	(
		decl = LocalVarDecl(false) { builder.add(decl); }
		( "," decl = LocalVarDecl(false) { builder.add(decl); })*
	)?
	{
		return builder.build();
	}
}

EntityDecl NetworkDecl() :
{
	Token id;
	ImmutableList<TypeDecl> typePars = ImmutableList.empty();
	ImmutableList<VarDecl> valuePars;
	ImmutableList<PortDecl> inputPorts;
	ImmutableList<PortDecl> outputPorts;
	ImmutableList.Builder<VarDecl> varDecls = ImmutableList.builder();
	ImmutableList.Builder<TypeDecl> typeDecls = ImmutableList.builder();
	ImmutableList<InstanceDecl> instances = ImmutableList.empty();
	ImmutableList<StructureStatement> structure = ImmutableList.empty();
	VarDecl varDecl;
}
{
	"network"
	id = <ID>
	"("
	valuePars = FormalValuePars()
	")"
	inputPorts = PortDecls()
	"==>"
	outputPorts = PortDecls()
	":"
	(
		"var"
		(
			varDecl = LocalVarDecl(true)
			{ varDecls.add(varDecl); }
		)*
	)?
	("entities" instances = EntityInstanceDecls())?
	("structure" structure = StructureStmts())?
	"end"


	{ return EntityDecl.global(Availability.PUBLIC, id.image, new NlNetwork(typePars, valuePars, typeDecls.build(), varDecls.build(), inputPorts, outputPorts,
                 			instances, structure)); }
}

ImmutableList<ToolAttribute> OptToolAttributes() :
{
	ImmutableList.Builder<ToolAttribute> builder = ImmutableList.builder();
	Token id;
	Expression expression;
	TypeExpr type;
}
{
	(
	"{"
	(
		id = <ID>
		(
			":"
			type = Type()
			{
				builder.add(new ToolTypeAttribute(id.image, type));
			}
		|
			"="
			expression = Expression()
			{
				builder.add(new ToolValueAttribute(id.image, expression));
			}
		)
		";"
	)*
	"}"
	)?
	{
		return builder.build();
	}
}

ImmutableList<InstanceDecl> EntityInstanceDecls() :
{
	Token instanceName;
	Token entityName;
	Token parameterName;
	Expression parameter;
	ImmutableList.Builder<InstanceDecl> entities = ImmutableList.builder();
	ImmutableList.Builder<ValueParameter> parameters;
	ImmutableList<ToolAttribute> toolAttrs;
}
{
	(
		{
			parameters = ImmutableList.builder();
		}
		instanceName = <ID>
		"="
		entityName = <ID>
		"("
		(

			parameterName = <ID>
			"="
			parameter = Expression()
			{
				parameters.add(new ValueParameter(parameterName.image, parameter));
			}
			(
				","
				parameterName = <ID>
				"="
				parameter = Expression()
				{
					parameters.add(new ValueParameter(parameterName.image, parameter));
				}
			)*
		)?
		")"
		toolAttrs = OptToolAttributes()
		";"
		{
			entities.add(new InstanceDecl(instanceName.image, new EntityInstanceExpr(new EntityReferenceLocal(entityName.image), parameters.build()).withAttributes(toolAttrs)));
		}
	)*
	{
		return entities.build();
	}
}

ImmutableList<StructureStatement> StructureStmts() :
{
	ImmutableList.Builder<StructureStatement> builder = ImmutableList.builder();
	Token src, srcPort, tgt, tgtPort;
	ImmutableList toolAttrs;
}
{
	(
		{ src = null; tgt = null; }
		(LOOKAHEAD(2) src = <ID> ".")?
		srcPort = <ID>
		"-->"
		(LOOKAHEAD(2) tgt = <ID> ".")?
		tgtPort = <ID>
		toolAttrs = OptToolAttributes()
		";"
		{
			builder.add(new StructureConnectionStmt(
					new PortReference(src == null ? null : src.image, ImmutableList.empty(), srcPort.image),
					new PortReference(tgt == null ? null : tgt.image, ImmutableList.empty(), tgtPort.image)
					).withAttributes(toolAttrs));
		}
	)*
	{
		return builder.build();
	}
}

/* ActorDecl */
EntityDecl ActorDecl() :
{
	Token id;
	String name;
	ImmutableList<TypeDecl> typePars = ImmutableList.empty();
	ImmutableList<VarDecl> valuePars;
	ImmutableList<TypeDecl> typeDecls = ImmutableList.empty();
	ImmutableList.Builder<VarDecl> varDecls = ImmutableList.builder();
	VarDecl varDecl;
	ImmutableList<PortDecl> inputPorts = ImmutableList.empty();
	ImmutableList<PortDecl> outputPorts = ImmutableList.empty();
	ImmutableList.Builder<Action> initializers = ImmutableList.builder();
	ImmutableList.Builder<Action> actions = ImmutableList.builder();
	Action action;
	ScheduleFSM scheduleFSM = null;
	ProcessDescription process = null;
	ImmutableList.Builder<ImmutableList<QID>> priorities = ImmutableList.builder();
	ImmutableList.Builder<Expression> invariants = ImmutableList.builder();
}
{
	("external")?
	"actor"
	id = <ID> { name = id.image; }
	"("
	valuePars = FormalValuePars()
	")"
	inputPorts = PortDecls()
	"==>"
	outputPorts = PortDecls()
	("time" Type())?
	(
		":"
		(
			(
				Annotation()
			)*
			(
				LOOKAHEAD((QID() ":")? "action")
				action = Action()
				{ actions.add(action); }
			|
				LOOKAHEAD((QID() ":")? "initialize")
				action = InitializationAction()
				{ initializers.add(action); }
			|
				varDecl = LocalVarDecl(true)
				{ varDecls.add(varDecl); }
			|
				PriorityOrder(priorities)
			|
				{
					if (scheduleFSM != null || process != null) {
						throw new ParseException("Only one action schedule or process description is allowed.");
					}
				}
				scheduleFSM = ActionSchedule()
			|
				{
					if (scheduleFSM != null || process != null) {
						throw new ParseException("Only one action schedule or process description is allowed.");
					}
				}
				process = ProcessDescription()
			)
		)*
		("end" | "endactor")
	|
		";"
	)
	{
		CalActor a = new CalActor(typePars, valuePars, typeDecls, varDecls.build(), inputPorts, outputPorts, initializers.build(), actions.build(), scheduleFSM, process, priorities.build(), invariants.build());
		return EntityDecl.global(Availability.PUBLIC, name, a);
	}
}

ProcessDescription ProcessDescription() :
{
	ImmutableList<Statement> statements;
	boolean repeated;
}
{
	(
		"repeat"
		{ repeated = true; }
	|
		"do"
		{ repeated = false; }
	)
	{
		processDescription = true;
	}
	statements = Statements()
	"end"
	{
		processDescription = false;
		return new ProcessDescription(statements, repeated);
	}
}

ImmutableList<PortDecl> PortDecls() :
{
	ImmutableList.Builder<PortDecl> decls = ImmutableList.builder();
	PortDecl decl;
}
{
	(
		decl = PortDecl() { decls.add(decl); }
		(
			","
			decl = PortDecl() { decls.add(decl); }
		)*
	)?
	{ return decls.build(); }
}

PortDecl PortDecl() :
{
	TypeExpr type = null;
	Token id;
}
{
	(
		Annotation()
	)*
	(
		LOOKAHEAD(2)
		type = Type()
		id = <ID>
		{ return new PortDecl(id.image, type); }
	|
		id = <ID>
		{ return new PortDecl(id.image, null); }
	)
}

/* Actions */

Action Action() :
{
	QID tag = null;
	ImmutableList<InputPattern> inputPatterns;
	ImmutableList<OutputExpression> outputExpressions;
	ImmutableList<TypeDecl> typeDecls = ImmutableList.empty();
	ImmutableList<VarDecl> varDecls = ImmutableList.empty();
	ImmutableList<Expression> guards = ImmutableList.empty();
	ImmutableList<Statement> body = ImmutableList.empty();
	Expression delay = null;
	ImmutableList<Expression> preconditions = ImmutableList.empty();
	ImmutableList<Expression> postconditions = ImmutableList.empty();
}
{
	( tag = QID() ":" )?
	"action"
	inputPatterns = InputPatterns()
	"==>"
	outputExpressions = OutputExpressions()
	("guard" guards = ExpressionList())?
//	("delay" delay = Expression())?
	("var" varDecls = VarDecls())?
	("do" body = Statements())?
	("end" | "endaction")
	{
		return new Action(tag, inputPatterns, outputExpressions, typeDecls, varDecls, guards, body, delay, preconditions, postconditions);
	}
}

Action InitializationAction() :
{
	QID tag = null;
	ImmutableList<InputPattern> inputPatterns = ImmutableList.empty();
	ImmutableList<OutputExpression> outputExpressions;
	ImmutableList<TypeDecl> typeDecls = ImmutableList.empty();
	ImmutableList<VarDecl> varDecls = ImmutableList.empty();
	ImmutableList<Expression> guards = ImmutableList.empty();
	ImmutableList<Statement> body = ImmutableList.empty();
	Expression delay = null;
	ImmutableList<Expression> preconditions = ImmutableList.empty();
	ImmutableList<Expression> postconditions = ImmutableList.empty();
}
{
	( tag = QID() ":" )?
	"initialize"
	"==>"
	outputExpressions = OutputExpressions()
	("guard" guards = ExpressionList())?
	("var" varDecls = VarDecls())?
//	("delay" delay = Expression())?
	("do" body = Statements())?
	("end" | "endinitialize")
	{
		return new Action(tag, inputPatterns, outputExpressions, typeDecls, varDecls, guards, body, delay, preconditions, postconditions);
	}
}

ImmutableList<InputPattern> InputPatterns() :
{
	ImmutableList.Builder<InputPattern> builder = ImmutableList.builder();
	InputPattern in;
}
{
	(
		in = InputPattern() { builder.add(in); }
		("," in = InputPattern() { builder.add(in); })*
	)?
	{ return builder.build(); }
}

InputPattern InputPattern() :
{
	Token id;
	Port port = null;
	ImmutableList.Builder<VarDecl> varDecls = ImmutableList.builder();
	Expression repeat = null;
}
{
	( id = <ID> ":" { port = new Port(id.image); })?
	"["
	(
		id = <ID> { varDecls.add(VarDecl.local(null, id.image, true, null)); }
		("," id = <ID> { varDecls.add(VarDecl.local(null, id.image, true, null)); } )*
	)?
	"]"
	("repeat" repeat = Expression())?
	{ return new InputPattern(port, varDecls.build(), repeat); }
}

ImmutableList<OutputExpression> OutputExpressions() :
{
	ImmutableList.Builder<OutputExpression> builder = ImmutableList.builder();
	OutputExpression out;
}
{
	(
		out = OutputExpression() { builder.add(out); }
		("," out = OutputExpression() { builder.add(out); })*
	)?
	{ return builder.build(); }
}

OutputExpression OutputExpression() :
{
	Token first;
	Token last;
	Token id = null;
	Port port = null;
	ImmutableList<Expression> exprs;
	Expression repeat = null;
}
{
	( id = <ID> ":" { port = new Port(id.image); })?
	first = "["
	exprs = ExpressionList()
	last = "]"
	( "repeat" repeat = Expression() )?
	{
		OutputExpression out = new OutputExpression(port, exprs, repeat);
		if (port != null) { first = id; }
		if (repeat != null) {
			return setPosition(out, first, repeat);
		} else {
			return setPosition(out, first, last);
		}
	}
}

/* Action control */
ScheduleFSM ActionSchedule() :
{
	Token init;
	ImmutableList<Transition> trans;
}
{
	"schedule"
	("fsm")?
	init = <ID>
	":"
	trans = StateTransitions()
	("end" | "endschedule")
	{ return new ScheduleFSM(trans, init.image); }
}

ImmutableList<Transition> StateTransitions() :
{
	ImmutableList.Builder<Transition> builder = ImmutableList.builder();
}
{
	(StateTransition(builder) ";")*
	{ return builder.build(); }
}

void StateTransition(ImmutableList.Builder<Transition> builder) :
{
	Token src;
	Token dst;
	QID qid;
	ImmutableList.Builder<QID> tags = ImmutableList.builder();
}
{
	src = <ID>
	"("
	qid = QID() { tags.add(qid); }
	( "," qid = QID() { tags.add(qid); } )*
	")"
	"-->"
	dst = <ID>
	{ builder.add(new Transition(src.image, dst.image, tags.build())); }
	(
		{ tags = ImmutableList.builder(); }
		"|"
		"("
		qid = QID() { tags.add(qid); }
		("," qid = QID() { tags.add(qid); } )*
		")"
		"-->"
		dst = <ID>
		{ builder.add(new Transition(src.image, dst.image, tags.build())); }
	)*
}

void PriorityOrder(ImmutableList.Builder<ImmutableList<QID>> prio) :
{
	ImmutableList.Builder<QID> seq;
	QID qid;
}
{
	"priority"
	(
		{ seq = ImmutableList.builder(); }
		qid = QID() { seq.add(qid); }
		">"
		qid = QID() { seq.add(qid); }
		(
			">"
			qid = QID() { seq.add(qid); }
		)*
		";"
		{ prio.add(seq.build()); }
	)*
	"end"
}

/* Expressions */

Expression Expression() :
{
	Expression expr;
	String op;
	ImmutableList.Builder<String> ops = ImmutableList.builder();
	ImmutableList.Builder<Expression> exprs = ImmutableList.builder();
}
{
	expr = UnaryOperation() { exprs.add(expr); }
	(
		op = BinaryOperator()
		expr = UnaryOperation()
		{ ops.add(op); exprs.add(expr); }
	)*
	{
		ImmutableList<String> opList = ops.build();
		ImmutableList<Expression> exprList = exprs.build();
		if (opList.isEmpty()) {
			return expr;
		} else {
			return setPosition(new ExprBinaryOp(opList, exprList), exprList.get(0), exprList.get(exprList.size()-1));
		}
	}
}

Expression UnaryOperation() :
{
	String op;
	List<String> ops = new ArrayList<String>();
	Expression expr;
}
{
	(
		op = UnaryOperator()
		{ ops.add(op); }
	)*
	expr = PrimaryExpression()
	{
		Collections.reverse(ops);
		for (String o : ops) {
			expr = new ExprUnaryOp(o, expr);
		}
		return expr;
	}
}

Expression PrimaryExpression() :
{
	Expression expr;
	ImmutableList<Expression> args;
	Expression index;
	Field field;
	Token lastToken;
}
{
	expr = SingleExpression()
	(
		"(" args = ExpressionList() lastToken = ")"
		{ expr = setPosition(new ExprApplication(expr, args), expr, lastToken); }
	|
		"["
		index = Expression()
		{ expr = new ExprIndexer(expr, index); }
		(
			","
			index = Expression()
			{ expr = new ExprIndexer(expr, index); }
		)*
		"]"
	|
		"." field = Field()
		{ expr = new ExprField(expr, field); }
	)*
	{
		return expr;
	}
}

Expression SingleExpression() :
{
	Expression expr;
}
{
	(
		expr = VariableExpression()
	|
		expr = ExpressionLiteral()
	|
		"(" expr = Expression() ")"
	|
		expr = IfExpression()
	|
		expr = LambdaExpression()
	|
		expr = ProcExpression()
	|
		expr = LetExpression()
	|
		expr = ListExpression()
	|
		expr = SetExpression()
	|
		expr = MapExpression()
	)
	{ return expr; }
}

ExprVariable VariableExpression() :
{
	Variable var;
}
{
	var = Variable()
	{
		return setPosition(new ExprVariable(var), var, var);
	}
}

Variable Variable() :
{
	Token id;
}
{
	id = <ID>
	{
		return setPosition(Variable.variable(id.image), id, id);
	}
}

ExprLiteral ExpressionLiteral() :
{
	Token lit;
	ExprLiteral result = null;
}
{
	(
		lit = <INTEGER> { result = new ExprLiteral(ExprLiteral.Kind.Integer, lit.image); }
	|
		lit = <REAL> { result = new ExprLiteral(ExprLiteral.Kind.Real, lit.image); }
	|
		lit = <TRUE> { result = new ExprLiteral(ExprLiteral.Kind.True); }
	|
		lit = <FALSE> { result = new ExprLiteral(ExprLiteral.Kind.False); }
	|
		lit = <NULL> { result = new ExprLiteral(ExprLiteral.Kind.Null); }
	|
		lit = <STRING> { result = new ExprLiteral(ExprLiteral.Kind.String, lit.image); }
	)
	{
		result.setPosition(lit.beginLine, lit.beginColumn, lit.endLine, lit.endColumn);
		return result;
	}
}


ImmutableList<Expression> ExpressionList() :
{
	ImmutableList.Builder<Expression> list = ImmutableList.builder();
	Expression expr;
}
{
	(
		expr = Expression()
		{ list.add(expr); }
		(
			","
			expr = Expression()
			{ list.add(expr); }
		)*
	)?
	{
		return list.build();
	}
}

String BinaryOperator() :
{
	Token op;
}
{
	(
		op = <OP> | op = "=" | op = "-->" | op = ">" | op = "|" | op = "*" | op = ".*" // | op = "@" | "->"
		| op = "and" | op = "or" | op = "in" | op = "div" | op = "mod"
	)
	{ return op.image; }
}

String UnaryOperator() :
{
	Token op;
}
{
	(
		op = <OP> | op = "=" | op = "-->" | op = "->" | op = ">" | op = "|" | op = "*" | op = ".*" // | op = "@"
		| op = "not" | op = "dom" | op = "rng" | op = "old"
	)
	{ return op.image; }
}


ExprIf IfExpression() :
{
	Expression cond;
	Expression thenExpr;
	Expression elseExpr;
}
{
	"if" cond = Expression()
	"then" thenExpr = Expression()
	(
		elseExpr = ElseIfExpression()
	|
		"else" elseExpr = Expression()
	)
	("end" | "endif")
	{
		return new ExprIf(cond, thenExpr, elseExpr);
	}
}

ExprIf ElseIfExpression() :
{
	Expression cond;
	Expression thenExpr;
	Expression elseExpr;
}
{
	"elsif"
	cond = Expression()
	"then"
	thenExpr = Expression()
	(
		elseExpr = ElseIfExpression()
	|
		"else"
		elseExpr = Expression()
	)
	{
		return new ExprIf(cond, thenExpr, elseExpr);
	}
}

ExprLet LetExpression() :
{
	ImmutableList<TypeDecl> typeDecls = ImmutableList.empty();
	ImmutableList<VarDecl> varDecls;
	Expression expr;
}
{
	<LET>
	varDecls = VarDecls()
	":"
	expr = Expression()
	("end" | "endlet")
	{ return new ExprLet(typeDecls, varDecls, expr); }
}


ExprLambda LambdaExpression() :
{
	ImmutableList<TypeDecl> typePars = ImmutableList.empty();
	ImmutableList<VarDecl> valuePars;
	TypeExpr type = null;
	Expression expr;
}
{
	<LAMBDA> "(" valuePars = FormalValuePars() ")"
	("-->" type = Type())?
	(
		{
			ImmutableList<TypeDecl> typeDecls = ImmutableList.empty();
			ImmutableList<VarDecl> varDecls;
		}
		"var" varDecls = VarDecls() ":" expr = Expression()
		{
			expr = new ExprLet(typeDecls, varDecls, expr);
		}
	|
		":" expr = Expression()
	)
	("end" | "endlambda")
	{
		return new ExprLambda(typePars, valuePars, expr, type, false);
	}
}


ImmutableList<VarDecl> FormalValuePars() :
{
	ImmutableList.Builder<VarDecl> builder = ImmutableList.builder();
	VarDecl decl;
}
{
	(
		decl = FormalValueParameter() { builder.add(decl); }
		( "," decl = FormalValueParameter() { builder.add(decl); } )*
	)?
	{
		return builder.build();
	}
}

VarDecl FormalValueParameter() :
{
	TypeAndName typeName;
}
{
	typeName = VarDeclTypeName()
	("=" Expression())?
	{
		return VarDecl.parameter(typeName.type, typeName.name);
	}
}


ExprProc ProcExpression() :
{
	ImmutableList<TypeDecl> typePars = ImmutableList.empty();
	ImmutableList<VarDecl> valuePars;
	ImmutableList<TypeDecl> typeDecls = ImmutableList.empty();
	ImmutableList<VarDecl> varDecls = ImmutableList.empty();
	ImmutableList<Statement> stmts;
}
{
	"proc" "(" valuePars = FormalValuePars() ")"
	( "var" varDecls = VarDecls() )?
	( "do" | "begin" )
	stmts = Statements()
	( "end" | "endproc" )
	{
		StmtBlock block = new StmtBlock(typeDecls, varDecls, stmts);
		return new ExprProc(typePars, valuePars, ImmutableList.of(block));
	}
}

Expression MapExpression() :
{
	Expression result;
	ImmutableList<ImmutableEntry<Expression, Expression>> mappings;
}
{
	"map"
	"{"
	mappings = Mappings()
	{ result = new ExprMap(mappings); }
	(
		":"
		result = ComprehensionFor(result)
	)?
	"}"
	{
		return result;
	}
}

ImmutableList<ImmutableEntry<Expression, Expression>> Mappings() :
{
	ImmutableList.Builder<ImmutableEntry<Expression, Expression>> builder = ImmutableList.builder();
	ImmutableEntry<Expression, Expression> mapping;
	Expression key;
	Expression value;
}
{
	(
		key = Expression() "->" value = Expression() { builder.add(ImmutableEntry.of(key, value)); }
		(
			","
			key = Expression() "->" value = Expression() { builder.add(ImmutableEntry.of(key, value)); }
		)*
	)?
	{ return builder.build(); }
}

Expression SetExpression() :
{
	Expression result;
	ImmutableList<Expression> exprs;
}
{
	"{"
	exprs = ExpressionList()
	{ result = new ExprSet(exprs); }
	(
		":"
		result = ComprehensionFor(result)
	)?
	"}"
	{
		return result;
	}
}

Expression ListExpression() :
{
	Expression result;
	ImmutableList<Expression> exprs;
}
{
	"["
	exprs = ExpressionList()
	{ result = new ExprList(exprs); }
	(
		":"
		result = ComprehensionFor(result)
	)?
	"]"
	{
		return result;
	}
}

Expression ComprehensionFor(Expression contents) :
{
	TypeExpr type = null;
	Token id;
	ImmutableList.Builder<VarDecl> varDecls = ImmutableList.builder();
	Expression collection;
	Expression filter;
	ImmutableList.Builder<Expression> filters = ImmutableList.builder();
}
{
	"for"
	(
		LOOKAHEAD(2)
		type = Type()
	)?
	id = <ID>
	{
		varDecls.add(VarDecl.local(type == null ? null : type.deepClone(), id.image, false, null));
	}
	(
		","
		id = <ID>
		{
			varDecls.add(VarDecl.local(type == null ? null : type.deepClone(), id.image, false, null));
		}
	)*
	"in"
	collection = Expression()
	(
		LOOKAHEAD(2)
		","
		filter = Expression()
		{ filters.add(filter); }
	)*
	(
		","
		contents = ComprehensionFor(contents)
	)?
	{
		return new ExprComprehension(new Generator(varDecls.build(), collection), filters.build(), contents);
	}
}


/* Type expressions */
TypeExpr Type() :
{
	Token name;
	TypeExpr type;
}
{
	name = <ID>
	type = ParameterizedType(name.image)
	{ return type; }
}

TypeExpr ParameterizedType(String name) :
{
	ImmutableList.Builder<TypeParameter> types = ImmutableList.builder();
	ImmutableList.Builder<ValueParameter> vals = ImmutableList.builder();
}
{
	(
		"("
		AddParameterAssignments(types, vals)
		("," AddParameterAssignments(types, vals))*
		")"
	)?
	{
		return new TypeExpr(name, types.build(), vals.build());
	}
}

void AddParameterAssignments(
	ImmutableList.Builder<TypeParameter> types,
	ImmutableList.Builder<ValueParameter> vals) :
{
	Token name;
	TypeExpr type;
	Expression expr;
}
{
	(
		LOOKAHEAD(2)
		(name = <ID> | name = <TYPE>)
		":" type = Type()
		{ types.add(new TypeParameter(name.image, type)); }
	|
		name = <ID>
		"=" expr = Expression()
		{ vals.add(new ValueParameter(name.image, expr)); }
	)
}

TypeAndName VarDeclTypeName() :
{
	Token id;
	TypeExpr type;
	Expression size;
}
{
	LOOKAHEAD(2)
	type = Type()
	id = <ID>
	{ return new TypeAndName(type, id.image); }
|
	id = <ID>
	{ return new TypeAndName(null, id.image); }
}


/* Statements */
Statement Statement() :
{
	Statement stmt;
}
{
	( Annotation() )*
	(
		LOOKAHEAD(LValue() ":=")
		stmt = AssignmentStmt()
	|
		//LOOKAHEAD(IfStmt()) to detect the procedure call "if x then p else q end (y)"
		LOOKAHEAD("if")
		stmt = IfStmt()
	|
		LOOKAHEAD(2, {processDescription})
		stmt = ReadStmt()
	|
		LOOKAHEAD(2, {processDescription})
		stmt = WriteStmt()
	|
		LOOKAHEAD(QID() ";", {processDescription})
		stmt = ActionSelectionStmt()
	|
		stmt = CallStmt()
	|
		stmt = BlockStmt()
	|
		stmt = WhileStmt()
	|
		stmt = ForeachStmt()
	)
	{
		return stmt;
	}
}

ImmutableList<Statement> Statements() :
{
	ImmutableList.Builder<Statement> stmts = ImmutableList.builder();
	Statement stmt;
}
{
	( stmt = Statement() { stmts.add(stmt); } )*
	{ return stmts.build(); }
}

Statement ReadStmt() :
{
	Token port;
	LValue lvalue;
	ImmutableList.Builder<LValue> lvalues = ImmutableList.builder();
	Expression repeatExpr = null;
}
{
	port = <ID>
	"-->"
	lvalue = LValue() { lvalues.add(lvalue); }
	(
		","
		lvalue = LValue() { lvalues.add(lvalue); }
	)*
	(
		"repeat"
		repeatExpr = Expression()
	)?
	";"
	{
		Port p = new Port(port.image);
		p.setPosition(port.beginLine, port.beginColumn, port.endLine, port.endColumn);
		return new StmtRead(p, lvalues.build(), repeatExpr);
	}
}

Statement WriteStmt() :
{
	Token port;
	Expression value;
	ImmutableList.Builder values = ImmutableList.builder();
	Expression repeatExpr = null;
}
{
	port = <ID>
	"<--"
	value = Expression() { values.add(value); }
	(
		","
		value = Expression() { values.add(value); }
	)*
	(
		"repeat"
		repeatExpr = Expression()
	)?
	";"
	{
		Port p = new Port(port.image);
		p.setPosition(port.beginLine, port.beginColumn, port.endLine, port.endColumn);
		return new StmtWrite(p, values.build(), repeatExpr);
	}
}

Statement ActionSelectionStmt() :
{
	QID tag;
}
{
	tag = QID() ";"
	{
		return null;
	}
}


StmtAssignment AssignmentStmt() :
{
	LValue lvalue;
	Expression rvalue;
	Token end;
}
{
	lvalue = LValue()
	":="
	rvalue = Expression()
	end = ";"
	{
		StmtAssignment result = new StmtAssignment(lvalue, rvalue);
		result.setPosition(lvalue.getFromLineNumber(), lvalue.getFromColumnNumber(), end.endLine, end.endColumn);
		return result;
	}
}

LValue LValue() :
{
	Variable var;
	Field field;
	Expression index;
	LValue lvalue;
	Token end;
}
{
	var = Variable()
	{
		lvalue = new LValueVariable(var);
		lvalue.setPosition(var, var);
	}
	(
		(
			"." field = Field()
			{
				lvalue = new LValueField(lvalue, field);
				lvalue.setPosition(var, field);
			}
		|
			"["
			index = Expression()
			end = "]"
			{
				lvalue = new LValueIndexer(lvalue, index);
				lvalue.setPosition(var.getFromLineNumber(), var.getFromColumnNumber(), end.endLine, end.endColumn);
			}

		)
	)*
	{
		return lvalue;
	}
}

Field Field() :
{
	Token id;
}
{
	id = <ID>
	{ return new Field(id.image); }
}

StmtCall CallStmt() :
{
	Expression proc;
	ImmutableList<Expression> args;
}
{
	proc = VariableExpression() // should be any expression, but is not because of conflict with annotations on statements
	"("
	args = ExpressionList()
	")"
	";"
	{
		return new StmtCall(proc, args);
	}
}

StmtBlock BlockStmt() :
{
	ImmutableList<VarDecl> varDecls = ImmutableList.empty();
	ImmutableList<Statement> stmts;
}
{
	"begin"
	(
		"var"
		varDecls = VarDecls()
		"do"
	)?
	stmts = Statements()
	"end"
	{
		return new StmtBlock(ImmutableList.empty(), varDecls, stmts);
	}
}

StmtIf IfStmt() :
{
	Expression cond;
	ImmutableList<Statement> thenStmts;
	ImmutableList<Statement> elseStmts = null;
}
{
	"if"
	cond = Expression()
	"then"
	thenStmts = Statements()
	(
		elseStmts = ElseIfStmt()
	|
		"else"
		elseStmts = Statements()
	)?
	"end"
	{
		return new StmtIf(cond, thenStmts, elseStmts);
	}
}

ImmutableList<Statement> ElseIfStmt() :
{
	Expression cond;
	Statement thenStmt;
	ImmutableList<Statement> thenStmts;
	Statement elseStmt;
	ImmutableList<Statement> elseStmts = null;
}
{
	"elsif"
	cond = Expression()
	"then"
	thenStmts = Statements()
	(
		elseStmts = ElseIfStmt()
	|
		"else"
		elseStmts = Statements()
	)?
	{
		return ImmutableList.of(new StmtIf(cond, thenStmts, elseStmts));
	}
}

StmtWhile WhileStmt() :
{
	Expression cond;
	ImmutableList<VarDecl> varDecls = ImmutableList.empty();
	ImmutableList<Statement> statements;
}
{
	"while"
	cond = Expression()
	(
		"var"
		varDecls = VarDecls()
	)?
	"do"
	statements = Statements()
	"end"
	{
		if (varDecls.isEmpty()) {
			return new StmtWhile(cond, statements);
		} else {
			return new StmtWhile(cond, ImmutableList.of(new StmtBlock(ImmutableList.empty(), varDecls, statements)));
		}
	}
}

StmtForeach ForeachStmt() :
{
	StmtForeach stmt;
}
{
	stmt = ForeachStmtHeader()
	"end"
	{
		return stmt;
	}
}

StmtForeach ForeachStmtHeader() :
{
	TypeExpr type = null;
	Token id;
	ImmutableList.Builder<VarDecl> varDecls = ImmutableList.builder();
	Expression collection;
	Expression filter;
	ImmutableList.Builder<Expression> filters = ImmutableList.builder();
	ImmutableList<Statement> body;
}
{
	"foreach"
	(
		LOOKAHEAD(2)
		type = Type()
	)?
	id = <ID>
	{ varDecls.add(VarDecl.local(type, id.image, false, null)); }
	(
		","
		id = <ID>
		{ varDecls.add(VarDecl.local(type.deepClone(), id.image, false, null)); }
	)*
	"in"
	 collection = Expression()
	(
		LOOKAHEAD(2)
		","
		filter = Expression()
		{ filters.add(filter); }
	)*
	body = ForeachStmtBody()
	{
		return new StmtForeach(new Generator(varDecls.build(), collection), filters.build(), body);
	}
}

ImmutableList<Statement> ForeachStmtBody() :
{
	Statement result;
	ImmutableList<VarDecl> varDecls = null;
	ImmutableList<Statement> stmts;
}
{
	"," result = ForeachStmtHeader()
	{
		return ImmutableList.of(result);
	}
|
	(
		"var"
		varDecls = VarDecls()
	)?
	"do"
	stmts = Statements()
	{
		if (varDecls == null) {
			return stmts;
		} else {
			return ImmutableList.of(new StmtBlock(ImmutableList.empty(), varDecls, stmts));
		}
	}
}


